marshalers = gnome.genmarshal('bz-marshalers',
  sources: 'bz-marshalers.list',
  prefix: 'bz_marshal',
  valist_marshallers: true,
)

gdbus_src = gnome.gdbus_codegen(
  'gs-shell-search-provider-generated',
  'shell-search-provider-dbus-interfaces.xml',
  interface_prefix : 'org.gnome.',
  namespace : 'Bz',
)

bz_sources = [
  'bz-addons-dialog.c',
  'bz-app-tile.c',
  'bz-application-map-factory.c',
  'bz-application.c',
  'bz-async-texture.c',
  'bz-backend.c',
  'bz-browse-widget.c',
  'bz-comet-overlay.c',
  'bz-comet.c',
  'bz-content-provider.c',
  'bz-content-section.c',
  'bz-data-graph.c',
  'bz-data-point.c',
  'bz-dynamic-list-view.c',
  'bz-entry-group.c',
  'bz-entry.c',
  'bz-env.c',
  'bz-error.c',
  'bz-flathub-category.c',
  'bz-flathub-page.c',
  'bz-flathub-state.c',
  'bz-flatpak-entry.c',
  'bz-flatpak-instance.c',
  'bz-full-view.c',
  'bz-global-progress.c',
  'bz-global-state.c',
  'bz-gnome-shell-search-provider.c',
  'bz-inhibited-scrollable.c',
  'bz-installed-page.c',
  'bz-issue.c',
  'bz-paintable-model.c',
  'bz-preferences-dialog.c',
  'bz-progress-bar.c',
  'bz-release.c',
  'bz-review.c',
  'bz-screenshot.c',
  'bz-search-engine.c',
  'bz-search-widget.c',
  'bz-section-view.c',
  'bz-share-dialog.c',
  'bz-stats-dialog.c',
  'bz-transaction-manager.c',
  'bz-transaction-view.c',
  'bz-transaction.c',
  'bz-update-dialog.c',
  'bz-url.c',
  'bz-window.c',
  'bz-yaml-parser.c',
  'main.c',
]

bz_deps = [
  cc.find_library('m', required: false),
  dependency('gtk4'),
  dependency('libadwaita-1', version: '>= 1.7'),
  dependency('libdex-1', version: '>= 0.9'),
  dependency('flatpak', version: '>= 1.9'),
  dependency('appstream', version : '>= 1.0'),
  dependency('xmlb', version : '>= 0.3.4'),
  dependency('glycin-1', version : '>= 1.0'),
  dependency('glycin-gtk4-1', version : '>= 1.0'),
  dependency('yaml-0.1', version : '>= 0.2.5'),
  dependency('libsoup-3.0', version : '>= 3.6.0'),
  dependency('json-glib-1.0', version : '>= 1.10.0'),
]

blueprints = custom_target('blueprints',
  input: files(
    'bz-addons-dialog.blp',
    'bz-app-tile.blp',
    'bz-browse-widget.blp',
    'bz-flathub-page.blp',
    'bz-full-view.blp',
    'bz-installed-page.blp',
    'bz-preferences-dialog.blp',
    'bz-progress-bar.blp',
    'bz-search-widget.blp',
    'bz-section-view.blp',
    'bz-share-dialog.blp',
    'bz-stats-dialog.blp',
    'bz-transaction-view.blp',
    'bz-update-dialog.blp',
    'bz-window.blp',
    'gtk/help-overlay.blp',
  ),
  output: '.',
  command: [
    find_program('blueprint-compiler', version: '>= 0.18.0'),
    'batch-compile',
    '@OUTPUT@',
    '@CURRENT_SOURCE_DIR@',
    '@INPUT@'
  ],
)

bz_sources += gnome.compile_resources('bz-resources',
  'bazaar.gresource.xml',
  c_name: 'bz',
  dependencies: blueprints
)

executable('bazaar', bz_sources, gdbus_src, marshalers,
  dependencies: bz_deps,
       install: true,
)
